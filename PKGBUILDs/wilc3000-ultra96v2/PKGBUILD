# Maintainer: Kenta Sato <tosainu.maple@gmail.com>

buildarch=8

pkgname=wilc3000-ultra96v2
_srctag=4cb2af86a94511a338e15fb0cbd0c666fcf98014
_srcname=linux-at91-${_srctag}
_kernel_ver=5.15.36-1
pkgver=16.0
pkgrel=1
pkgdesc="ATWILC1000 & ATWILC3000 Wireless Drivers for Ultra96-V2"
arch=('aarch64')
url="https://github.com/linux4sam/linux-at91"
license=('GPL2')
makedepends=("linux-zynqmp-headers=${_kernel_ver}")
source=("https://github.com/Tosainu/linux-at91/archive/${_srctag}.tar.gz"
        'wilc_sdio.conf')
md5sums=('f00602b9931cf7bdb2da4ea3b2930a92'
         '8e9746265d41b4d4336a037c9ab7b755')

build() {
  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  unset LDFLAGS
  make ${MAKEFLAGS} -C "/usr/lib/modules/${_kernel_ver}-zynqmp-ARCH/build" \
    M=$PWD \
    CONFIG_WILC=y \
    WLAN_VENDOR_MCHP=y \
    CONFIG_WILC_SDIO=m \
    CONFIG_WILC_SPI=n \
    CONFIG_WILC_HW_OOB_INTR=n
}

package() {
  depends=("linux-zynqmp=${_kernel_ver}")

  cd "${srcdir}"

  install -Dt "${pkgdir}/etc/modules-load.d" -m644 wilc_sdio.conf

  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  install -Dt "${pkgdir}/usr/lib/modules/${_kernel_ver}-zynqmp-ARCH/extramodules" -m644 wilc-sdio.ko
}
