# Maintainer: Kenta Sato <tosainu.maple@gmail.com>

buildarch=8

pkgname=wilc3000-ultra96v2
_srctag=4ebc07781ff8bb342937d44ccb0e1818df974d35
_srcname=linux-at91-${_srctag}
_kernel_ver=6.6.10-1
pkgver=16.2
pkgrel=1
pkgdesc="ATWILC1000 & ATWILC3000 Wireless Drivers for Ultra96-V2"
arch=('aarch64')
url="https://github.com/linux4sam/linux-at91"
license=('GPL2')
makedepends=("linux-zynqmp-headers=${_kernel_ver}")
source=("https://github.com/Tosainu/linux-at91/archive/${_srctag}.tar.gz"
        'wilc_sdio.conf')
md5sums=('9248f619f8a7c4565b6449a093851e9e'
         '8e9746265d41b4d4336a037c9ab7b755')

build() {
  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  unset LDFLAGS
  make ${MAKEFLAGS} -C "/usr/lib/modules/${_kernel_ver}-zynqmp/build" \
    M=$PWD \
    CONFIG_WILC=y \
    WLAN_VENDOR_MCHP=y \
    CONFIG_WILC_SDIO=m \
    CONFIG_WILC_SPI=n \
    CONFIG_WILC_HW_OOB_INTR=n
}

package() {
  depends=("linux-zynqmp=${_kernel_ver}")

  cd "${srcdir}"

  install -Dt "${pkgdir}/etc/modules-load.d" -m644 wilc_sdio.conf

  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  install -Dt "${pkgdir}/usr/lib/modules/${_kernel_ver}-zynqmp/extramodules" -m644 wilc-sdio.ko
}
