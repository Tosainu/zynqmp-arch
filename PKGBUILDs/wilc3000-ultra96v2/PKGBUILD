# Maintainer: Kenta Sato <tosainu.maple@gmail.com>

buildarch=8

pkgname=wilc3000-ultra96v2
_srctag=2ca31a6563ac5eeb0eaecc0155b479a65fd11c6e
_srcname=linux-at91-${_srctag}
_kernel_ver=5.15.0-1
pkgver=15.6
pkgrel=2
pkgdesc="ATWILC1000 & ATWILC3000 Wireless Drivers for Ultra96-V2"
arch=('aarch64')
url="https://github.com/linux4sam/linux-at91"
license=('GPL2')
makedepends=("linux-zynqmp-headers=${_kernel_ver}")
source=("https://github.com/Tosainu/linux-at91/archive/${_srctag}.tar.gz"
        'wilc_sdio.conf')
md5sums=('10b2176dc4349bd6825002f84b4686be'
         '8e9746265d41b4d4336a037c9ab7b755')

build() {
  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  unset LDFLAGS
  make ${MAKEFLAGS} -C "/usr/lib/modules/${_kernel_ver}-zynqmp-ARCH/build" \
    M=$PWD \
    CONFIG_WILC=y \
    WLAN_VENDOR_MCHP=y \
    CONFIG_WILC_SDIO=m \
    CONFIG_WILC_SPI=n \
    CONFIG_WILC_HW_OOB_INTR=n
}

package() {
  depends=("linux-zynqmp=${_kernel_ver}")

  cd "${srcdir}"

  install -Dt "${pkgdir}/etc/modules-load.d" -m644 wilc_sdio.conf

  cd "${srcdir}/${_srcname}/drivers/net/wireless/microchip/wilc1000"

  install -Dt "${pkgdir}/usr/lib/modules/${_kernel_ver}-zynqmp-ARCH/extramodules" -m644 wilc-sdio.ko
}
